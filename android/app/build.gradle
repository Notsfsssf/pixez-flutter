/*
 * Copyright (C) 2020. by perol_notsf, All rights reserved
 *
 * This program is free software: you can redistribute it and/or modify it under
 * the terms of the GNU General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program. If not, see <http://www.gnu.org/licenses/>.
 *
 */

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def dartEnvironmentVariables = [
        IS_GOOGLEPLAY: false,
];

if (project.hasProperty('dart-defines')) {
    dartEnvironmentVariables = dartEnvironmentVariables + project.property('dart-defines')
            .split(',')
            .collectEntries { entry ->
                def pair = new String(entry.decodeBase64(), 'UTF-8').split('=')
                [(pair.first()): pair.last() == "true"]
            }
}
print("\n" +
        "______ _______   __ _____ ______\n" +
        "| ___ \\_   _\\ \\ / /|  ___|___  /\n" +
        "| |_/ / | |  \\ V / | |__    / / \n" +
        "|  __/  | |  /   \\ |  __|  / /  \n" +
        "| |    _| |_/ /^\\ \\| |___./ /___\n" +
        "\\_|    \\___/\\/   \\/\\____/\\_____/\n" +
        "                                \n" +
        "                                \n")
println("hey,IS_GOOGLEPLAY=${dartEnvironmentVariables.IS_GOOGLEPLAY}")
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
def packageName = dartEnvironmentVariables.IS_GOOGLEPLAY ? 'com.perol.play.pixez' : 'com.perol.pixez'
println(packageName)
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 33

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId packageName
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 10009330
        versionName "0.9.33 rule"
        ndk { abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64' }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    android {
        splits {
            abi {
                enable true
                reset()
                include 'armeabi-v7a', 'arm64-v8a', 'x86_64'
                universalApk true
            }
        }
    }
    lint {
        disable 'InvalidPackage'
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation("androidx.core:core-remoteviews:1.0.0-beta03")
    implementation("androidx.annotation:annotation:1.5.0")
    implementation fileTree(dir: "libs", include: ["*.aar"])
    implementation project(":weiss")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1")
    implementation("com.google.android.material:material:1.8.0-rc01")
    implementation("io.coil-kt:coil:2.1.0")
    implementation("androidx.webkit:webkit:1.4.0")
    implementation("androidx.browser:browser:1.4.0")
    implementation("com.waynejo:androidndkgif:0.3.3")
    implementation("androidx.preference:preference-ktx:1.2.0")
    implementation("com.google.android.play:core:1.10.3")
    implementation("androidx.documentfile:documentfile:1.0.1")
}
